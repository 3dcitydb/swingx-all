plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'org.citydb'
version '1.6.5-2'
description 'Fork of the inactive swingx-all library'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile 'org.swinglabs.swingx:swingx-graphics:1.6.5-1'
    compile 'org.swinglabs.swingx:swingx-core:1.6.5-1'
    compile 'org.swinglabs.swingx:swingx-mavensupport:1.6.5-1'
    compileOnly 'org.kohsuke.metainf-services:metainf-services:1.3'
    annotationProcessor 'org.kohsuke.metainf-services:metainf-services:1.3'
}

javadoc {
    failOnError = false
    options.addStringOption('Xdoclint:none', '-quiet')
}

jar {
    from(sourceSets.main.allSource) {
        include '**/*'
    }
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/3dcitydb'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License (LGPL), Version 2.1'
                        url = 'http://www.fsf.org/licensing/licenses/lgpl.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/3dcitydb/swingx-all'
                    connection = 'scm:git:https://github.com/3dcitydb/swingx-all.git'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        userOrg = '3dcitydb'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/3dcitydb'
        vcsUrl = 'https://github.com/3dcitydb/swingx-all.git'
        issueTrackerUrl = 'https://github.com/3dcitydb/swingx-all/issues'
        licenses = ['LGPL-2.1']
        version {
            name = project.version
        }
    }
}