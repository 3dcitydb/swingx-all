plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'maven-publish'

group 'org.citydb'
version '1.6.5-2'
description 'Fork of the inactive swingx-all library'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile 'org.kohsuke.metainf-services:metainf-services:1.3'
    compile 'org.swinglabs.swingx:swingx-graphics:1.6.5-1'
    compile 'org.swinglabs.swingx:swingx-core:1.6.5-1'
    compile 'org.swinglabs.swingx:swingx-mavensupport:1.6.5-1'
}

javadoc {
    failOnError = false
}

jar {
    from(sourceSets.main.allSource) {
        include '**/*'
    }
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def createPom = {
    name project.name
    url 'https://github.com/3dcitydb'
    licenses {
        license {
            name 'GNU Lesser General Public License (LGPL), Version 2.1'
            url 'http://www.fsf.org/licensing/licenses/lgpl.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'clausnagel'
            name 'Claus Nagel'
            email 'claus.nagel@gmail.com'
            roles {
                role 'architect'
                role 'developer'
            }
            timezone '+1'
        }
    }
    scm {
        url 'https://github.com/3dcitydb/swingx-all'
        connection 'scm:git:https://github.com/3dcitydb/swingx-all.git'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + createPom
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        userOrg = '3dcitydb'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/3dcitydb'
        vcsUrl = 'https://github.com/3dcitydb/swingx-all.git'
        issueTrackerUrl = 'https://github.com/3dcitydb/swingx-all/issues'
        labels = ['swingx-all', '3dcitydb']
        licenses = ['LGPL-2.1']
        version {
            name = project.version
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5'
}